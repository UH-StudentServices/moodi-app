/*
 * This file is part of Moodi application.
 *
 * Moodi application is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Moodi application is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Moodi application.  If not, see <http://www.gnu.org/licenses/>.
 */

//
// Publishing build artifacts.
//

apply plugin: "maven-publish"

publishing {

    //
    // What is published, i.e. build artifacts and their names.
    //
    publications {
        maven(MavenPublication) {
            groupId "fi.helsinki.moodi"
            artifactId "moodi"
            version version
            from components.java
        }
    }

    //
    // Where to publish, i.e. repository connection information.
    //
    repositories {
        maven {
            credentials {
                username opintoni_artifactory_username
                password opintoni_artifactory_password
            }

            if (version.endsWith("SNAPSHOT")) {
                url "${opintoni_artifactory_base_url}/libs-snapshot-local"
            } else {
                url "${opintoni_artifactory_base_url}/libs-release-local"
            }
        }
    }
}

// Run tests before publishing
tasks.withType(PublishToMavenRepository) {
    it.dependsOn test
}

// Ensure that jar produced by bootRepackace is published
publish.dependsOn bootJar